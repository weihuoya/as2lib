\chapter{Output Handling}
\label{sec:OutputHandling}

\paragraph{Motivation:}
The normal output of applications in Flash is done via the native operation
\begin{lstlisting}[frame=single]
trace(expression);
\end{lstlisting}
The trace output is nevertheless only visible inside the development environment that supports the operation. In all other cases (e.g.: in a web application) no standard output is defined. A library should provide a standardized output for users as well as for the developer that can be used everywhere.

\paragraph{Solution:}
To have multiple output possibilities in every runtime environment\footnote{Flash can run in a Browser, Flash Player, in {\sl Macromedia Central}, in a compiled application(*.exe),...}, the \emph{Out}\footnote{org.as2lib.env.out.Out} class is used. Given this class, it is possible to e.g.: save error messages on the server side, to ensure that failures are not only perceptible to the client but also to the developer. The \emph{Out} class deals with all incoming requests and forwards them depending on the configuration to one or more \emph{OutputHandler}\footnote{org.as2lib.env.out.OutputHandler, org.as2lib.env.out.handler.*}. {\sl As2lib} offers a standardized output possibility for unlimited interfaces.

\paragraph{Usage:}

A simple use case of the {\sl as2lib} Output Handling is visualized in figure \ref{fig:outHandling} on page \pageref{fig:outHandling}. After an instance of the \emph{Out} class is created and the provided TraceHandler\footnote{org.as2lib.env.out.handler.TraceHandler} added, an output can take place. The sequence of the single actions matches the order in which they are numbered.

\begin{figure}[!ht]
\begin{center}
\includegraphics{uml/as2lib/out.eps}
\caption{Use case of the {\sl as2lib} Output Handling}
\label{fig:outHandling}
\end{center}
\end{figure}

The \emph{Out} class can as in figure \ref{fig:outHandling} on page \pageref{fig:outHandling} access a pre-defined output handler like the \emph{TraceHandler} or a custom implementation of the \emph{OutHandler} (e.g.: an output in the {\sl Macromedia} Alert Component)\footnote{The Macromedia Alert Component must be in the library and you must own Flash MX Professional 2004, to access it via the Alert class.}:

\begin{lstlisting}[frame=single]
import org.as2lib.env.event.EventInfo;
import org.as2lib.env.out.OutHandler;
import org.as2lib.env.out.OutInfo;
import org.as2lib.env.out.OutConfig;
import org.as2lib.core.BasicClass;
import mx.controls.Alert;

class test.org.as2lib.env.out.handler.UIAlertHandler 
	extends BasicClass implements OutHandler {

	public function write(info:OutInfo):Void {
		Alert.show(info.getMessage(), 
			getClass().getName());
	}

}
\end{lstlisting}

The definition of the output levels (e.g.: \emph{out.setLevel(Out.DEBUG)}) makes it possible to prohibit the output of certain information.
The possible output levels are:
\begin{itemize}
	\item Out.ALL
	\item Out.DEBUG
	\item Out.INFO
	\item Out.WARNING
	\item Out.ERROR
	\item Out.FATAL
	\item Out.NONE
\end{itemize}

These levels allow developers to "switch on" display of debug messages during development or maintenance, these messages can then be easily "switched off" or "filtered" before publishing the final application. If during development, you wish to view all types of messages above \textit{DEBUG}, you would set the level to \emph{Out.DEBUG}, This way all messages of type \textit{DEBUG}, \textit{INFO}, \textit{WARNING}, \textit{ERROR} and \textit{FATAL} are displayed, while all \textit{LOG} messages are supressed. 

\begin{lstlisting}[frame=single]
var out:Out = new Out();
out.addHandler(new TraceHandler());

out.setLevel(Out.DEBUG);

out.log("log me Please!");
out.debug("debug me Please!");
out.info("inform me Please!");
out.warning("warn me Please!");
out.error(new Exception("Output Error", this, 
  arguments));
out.fatal(new FatalException("Fatal Output Error",
	this, arguments));
\end{lstlisting}

Should you choose to display only fatal failures in your finished application, this can be accomplished by changing a single line.

\begin{lstlisting}[frame=single]
out.setLevel(Out.FATAL);
\end{lstlisting}
